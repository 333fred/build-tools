task ntcoreDesktopZip(type: Zip) {
    description = 'Creates the ntcore-desktop.zip file, with the contents of all zip files in ntcore-zip'
    group = 'ntcore'
    baseName = 'ntcore'
    classifier = 'desktop'
    destinationDir = buildDir

    fileTree(dir: 'ntcore-zip', include: '*.zip').each { zip ->
        from zipTree(zip)
    }
}

task ntcoreDesktopJar(type: Jar) {
    description = 'Creates the ntcore-desktop.jar file, with the contents of all zip and jar files in ntcore-jar'
    group = 'ntcore'
    baseName = 'ntcore'
    classifier = 'desktop'
    destinationDir = buildDir

    fileTree(dir: 'ntcore-jar', include: ['*.zip', '*.jar'], exclude: ['*javadoc*', '*sources*']).each { zip ->
        from zipTree(zip)
    }
}

publishing {
    publications {
        ntcoreZip(MavenPublication) {
            artifact ntcoreDesktopZip

            groupId 'edu.wpi.first.wpilib.networktables.cpp'
            artifactId 'NetworkTables'
            version '3.0.0-SNAPSHOT'
        }
        ntcoreJar(MavenPublication) {
            artifact ntcoreDesktopJar

            groupId 'edu.wpi.first.wpilib.networktables.java'
            artifactId 'NetworkTables'
            version '3.0.0-SNAPSHOT'
        }
    }
}
